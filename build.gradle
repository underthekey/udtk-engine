plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'checkstyle'
}

allprojects {
    group = 'site.udtk.engine'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    ext {
        springModulithVersion = "1.2.1"
        springCloudVersion = "2023.0.3"
    }

    apply plugin: 'io.spring.dependency-management'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    checkstyle {
        maxWarnings = 0
        configFile = file("${rootProject.projectDir}/config/checkstyle/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${rootProject.projectDir}/config/checkstyle/naver-checkstyle-supressions.xml"]
        toolVersion = "8.44"
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.modulith:spring-modulith-starter-core'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    if (project.name != 'eureka-server') {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        }
    }

    test {
        useJUnitPlatform()
    }

    bootJar { enabled = false }

    jar { enabled = true }
}