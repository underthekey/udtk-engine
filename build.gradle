plugins {
    id 'java'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.3.2' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'site.udtk.engine'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    checkstyle {
        maxWarnings = 0
        configFile = file("${rootDir}/config/naver-checkstyle-rules.xml")
        configProperties = ["suppressionFile": "${rootDir}/config/naver-checkstyle-suppressions.xml"]
        toolVersion = "8.44"
    }

    repositories {
        mavenCentral()
    }

    ext {
        set('springModulithVersion', "1.2.1")
        set('springCloudVersion', "2023.0.3")
    }

}

subprojects {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.modulith:spring-modulith-starter-core'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    if (project.name != 'eureka-server') {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    bootJar { enabled = false }
}

